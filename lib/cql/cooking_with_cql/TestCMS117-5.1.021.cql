library TestCMS117 version '5.1.021'

using QDM version '5.3'

valueset "ONC Administrative Sex": 'urn:oid:2.16.840.1.113762.1.4.1'
valueset "Race": 'urn:oid:2.16.840.1.114222.4.11.836'
valueset "Ethnicity": 'urn:oid:2.16.840.1.114222.4.11.837'
valueset "Payer": 'urn:oid:2.16.840.1.114222.4.11.3591'
valueset "Anaphylactic Reaction to DTaP Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1031'
valueset "Anaphylactic Reaction to Hemophilus Influenza B (HiB) Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1030'
valueset "Anaphylactic Reaction to Hepatitis A Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1026'
valueset "Anaphylactic Reaction to Hepatitis B Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1029'
valueset "Anaphylactic Reaction to Inactivated Polio Vaccine (IPV)": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1023'
valueset "Anaphylactic Reaction to Influenza Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1022'
valueset "Anaphylactic Reaction to Neomycin": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1024'
valueset "Anaphylactic Reaction to Pneumococcal Conjugate Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1027'
valueset "Anaphylactic Reaction to Polymyxin": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1025'
valueset "Anaphylactic Reaction to Rotavirus Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1021'
valueset "Anaphylactic Reaction to Streptomycin": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1028'
valueset "Disorders of the Immune System": 'urn:oid:2.16.840.1.113883.3.464.1003.120.12.1001'
valueset "DTaP Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1214'
valueset "DTaP Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1022'
valueset "Encephalopathy due to Childhood Vaccination": 'urn:oid:2.16.840.1.113883.3.464.1003.114.12.1007'
valueset "Face-to-Face Interaction": 'urn:oid:2.16.840.1.113883.3.464.1003.101.12.1048'
valueset "Haemophilus Influenzae Type B (HiB) Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1217'
valueset "Haemophilus Influenzae Type B (HiB) Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1043'
valueset "Hepatitis A": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1024'
valueset "Hepatitis A Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1215'
valueset "Hepatitis A Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1041'
valueset "Hepatitis B": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1025'
valueset "Hepatitis B Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1216'
valueset "Hepatitis B Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1042'
valueset "HIV": 'urn:oid:2.16.840.1.113883.3.464.1003.120.12.1003'
valueset "Home Healthcare Services": 'urn:oid:2.16.840.1.113883.3.464.1003.101.12.1016'
valueset "Inactivated Polio Vaccine (IPV)": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1219'
valueset "Inactivated Polio Vaccine (IPV) Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1045'
valueset "Influenza Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1218'
valueset "Influenza Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1044'
valueset "Malignant Neoplasm of Lymphatic and Hematopoietic Tissue": 'urn:oid:2.16.840.1.113883.3.464.1003.108.12.1009'
valueset "Measles": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1053'
valueset "Measles Antibody Test (IgG Antibody presence)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1060'
valueset "Measles Antibody Test (IgG Antibody Titer)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1059'
valueset "Measles, Mumps and Rubella (MMR) Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1224'
valueset "Measles, Mumps and Rubella (MMR) Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1031'
valueset "Mumps": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1032'
valueset "Mumps Antibody Test (IgG Antibody presence)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1062'
valueset "Mumps Antibody Test (IgG Antibody Titer)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1061'
valueset "Office Visit": 'urn:oid:2.16.840.1.113883.3.464.1003.101.12.1001'
valueset "Pneumococcal Conjugate Vaccine": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1221'
valueset "Pneumococcal Conjugate Vaccine Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1046'
valueset "Positive Finding": 'urn:oid:2.16.840.1.113883.3.464.1003.121.12.1016'
valueset "Preventive Care, Established Office Visit, 0 to 17": 'urn:oid:2.16.840.1.113883.3.464.1003.101.12.1024'
valueset "Preventive Care Services, Initial Office Visit, 0 to 17": 'urn:oid:2.16.840.1.113883.3.464.1003.101.12.1022'
valueset "Rotavirus Vaccine (2 dose schedule)": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1222'
valueset "Rotavirus Vaccine (2 dose schedule) Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1048'
valueset "Rotavirus Vaccine (3 dose schedule)": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1223'
valueset "Rotavirus Vaccine (3 dose schedule) Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1047'
valueset "Rubella": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1037'
valueset "Rubella Antibody Test (IgG Antibody presence)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1064'
valueset "Rubella Antibody Test (IgG Antibody Titer)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1063'
valueset "Varicella Zoster": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1039'
valueset "Varicella Zoster Antibody Test (IgG Antibody Presence)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1067'
valueset "Varicella Zoster Antibody Test (IgG Antibody Titer)": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1066'
valueset "Varicella Zoster Vaccine (VZV)": 'urn:oid:2.16.840.1.113883.3.464.1003.196.12.1170'
valueset "Varicella Zoster Vaccine (VZV) Administered": 'urn:oid:2.16.840.1.113883.3.464.1003.110.12.1040'
valueset "Encounter Inpatient": 'urn:oid:2.16.840.1.113883.3.666.5.307'
valueset "Hospice care ambulatory": 'urn:oid:2.16.840.1.113762.1.4.1108.15'
valueset "Discharged to Home for Hospice Care": 'urn:oid:2.16.840.1.113883.3.117.1.7.1.209'
valueset "Discharged to Health Care Facility for Hospice Care": 'urn:oid:2.16.840.1.113883.3.117.1.7.1.207'
valueset "Anaphylactic Reaction to Common Baker's Yeast": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1032'
valueset "Intussusception": 'urn:oid:2.16.840.1.113883.3.464.1003.199.12.1056'
valueset "Severe Combined Immunodeficiency": 'urn:oid:2.16.840.1.113883.3.464.1003.120.12.1007'
valueset "Anti Hepatitis B Virus Surface Ab": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1073'
valueset "Anti Hepatitis A IgG Antigen Test": 'urn:oid:2.16.840.1.113883.3.464.1003.198.12.1033'

parameter "Measurement Period" Interval<DateTime>

context Patient

define "SDE Ethnicity":
	["Patient Characteristic Ethnicity": "Ethnicity"]

define "SDE Payer":
	["Patient Characteristic Payer": "Payer"]

define "SDE Race":
	["Patient Characteristic Race": "Race"]

define "SDE Sex":
	["Patient Characteristic Sex": "ONC Administrative Sex"]

define "Denominator":
	"Initial Population"

define "Denominator Exclusions":
	exists ( ["Encounter, Performed": "Encounter Inpatient"] DischargeHospice
			where ( ( DischargeHospice.dischargeDisposition in "Discharged to Home for Hospice Care"
						or DischargeHospice.dischargeDisposition in "Discharged to Health Care Facility for Hospice Care"
				)
					and DischargeHospice.relevantPeriod ends during "Measurement Period"
			)
	)
		or exists ( ["Intervention, Order": "Hospice care ambulatory"] HospiceOrder
				where HospiceOrder.authorDatetime during "Measurement Period"
		)
		or exists ( ["Intervention, Performed": "Hospice care ambulatory"] HospicePerformed
				where HospicePerformed.relevantPeriod overlaps "Measurement Period"
		)

define "DTaP Immunizations or Procedures":
	( ["Immunization, Administered": "DTaP Vaccine"]
		union ( ["Procedure, Performed": "DTaP Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) DTaPVaccination
		where AgeInDaysAt(DTaPVaccination.authorDatetime)>= 42
			and AgeInDaysAt(DTaPVaccination.authorDatetime)<= 730

define "DTaP Vaccinations":
	from
		"DTaP Immunizations or Procedures" Vaccination1,
		"DTaP Immunizations or Procedures" Vaccination2,
		"DTaP Immunizations or Procedures" Vaccination3,
		"DTaP Immunizations or Procedures" Vaccination4
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
			and Vaccination4.authorDatetime 1 day or more after Vaccination3.authorDatetime
		return Vaccination1

define "Hepatitis A Vaccinations":
	( ["Immunization, Administered": "Hepatitis A Vaccine"]
		union ( ["Procedure, Performed": "Hepatitis A Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) HepAVaccination
		where AgeInDaysAt(HepAVaccination.authorDatetime)<= 730

define "Hepatitis B Immunizations or Procedures":
	( ["Immunization, Administered": "Hepatitis B Vaccine"]
		union ( ["Procedure, Performed": "Hepatitis B Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) HepBVaccination
		where AgeInDaysAt(HepBVaccination.authorDatetime)<= 730

define "Hepatitis B Vaccinations":
	from
		"Hepatitis B Immunizations or Procedures" Vaccination1,
		"Hepatitis B Immunizations or Procedures" Vaccination2,
		"Hepatitis B Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "HIB Immunizations or Procedures":
	( ["Immunization, Administered": "Haemophilus Influenzae Type B (HiB) Vaccine"] Vaccine
		union ( ["Procedure, Performed": "Haemophilus Influenzae Type B (HiB) Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) HiBVaccination
		where AgeInDaysAt(HiBVaccination.authorDatetime)>= 42
			and AgeInDaysAt(HiBVaccination.authorDatetime)<= 730

define "HIB Vaccinations":
	from
		"HIB Immunizations or Procedures" Vaccination1,
		"HIB Immunizations or Procedures" Vaccination2,
		"HIB Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Influenza Immunizations or Procedures":
	( ["Immunization, Administered": "Influenza Vaccine"]
		union ( ["Procedure, Performed": "Influenza Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) FluVaccination
		where AgeInDaysAt(FluVaccination.authorDatetime)>= 180
			and AgeInDaysAt(FluVaccination.authorDatetime)<= 730

define "Influenza Vaccinations":
	from
		"Influenza Immunizations or Procedures" Vaccination1,
		"Influenza Immunizations or Procedures" Vaccination2
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
		return Vaccination1

define "Initial Population":
	exists ( "Qualifying Encounter" )
		and AgeInYearsAt(start of "Measurement Period")>= 1
		and AgeInYearsAt(end of "Measurement Period")= 2

define "IVP Immunizations or Procedures":
	( ["Immunization, Administered": "Inactivated Polio Vaccine (IPV)"]
		union ( ["Procedure, Performed": "Inactivated Polio Vaccine (IPV) Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) IVPVaccination
		where AgeInDaysAt(IVPVaccination.authorDatetime)>= 42
			and AgeInDaysAt(IVPVaccination.authorDatetime)<= 730

define "IVP Vaccinations":
	from
		"IVP Immunizations or Procedures" Vaccination1,
		"IVP Immunizations or Procedures" Vaccination2,
		"IVP Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Measles Indicators":
	( ( ["Laboratory Test, Performed": "Measles Antibody Test (IgG Antibody Titer)"] Titer
			where Titer.result >= 1.10
	)
		union ( ["Laboratory Test, Performed": "Measles Antibody Test (IgG Antibody presence)"] IgGPresence
				where IgGPresence.result as Code in "Positive Finding"
		)
		union ( ["Diagnosis": "Measles"] MeaslesDiagnosis
				return "Laboratory Test, Performed" { authorDatetime: start of MeaslesDiagnosis.prevalencePeriod }
		) ) Measles
		where AgeInDaysAt(Measles.authorDatetime)<= 730

define "MMR Vaccinations":
	( ["Immunization, Administered": "Measles, Mumps and Rubella (MMR) Vaccine"]
		union ( ["Procedure, Performed": "Measles, Mumps and Rubella (MMR) Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) MMRVaccination
		where AgeInDaysAt(MMRVaccination.authorDatetime)<= 730

define "Mumps Indicators":
	( ( ["Laboratory Test, Performed": "Mumps Antibody Test (IgG Antibody Titer)"] Titer
			where Titer.result >= 1.10
	)
		union ( ["Laboratory Test, Performed": "Mumps Antibody Test (IgG Antibody presence)"] IgGPresence
				where IgGPresence.result as Code in "Positive Finding"
		)
		union ( ["Diagnosis": "Mumps"] MumpsDiagnosis
				return "Laboratory Test, Performed" { authorDatetime: start of MumpsDiagnosis.prevalencePeriod }
		) ) Mumps
		where AgeInDaysAt(Mumps.authorDatetime)<= 730

define "Numerator":
	( exists ( "DTaP Vaccinations" )
			or exists ( "Numerator Compliant DTaP Conditions" )
	)
		and ( exists ( "IVP Vaccinations" )
				or exists ( "Numerator Compliant IVP Conditions" )
		)
		and ( exists ( "MMR Vaccinations" )
				or exists ( "Numerator Compliant MMR Conditions" )
				or ( exists ( "Measles Indicators" )
						and exists ( "Mumps Indicators" )
						and exists ( "Rubella Indicators" )
				)
		)
		and ( exists ( "HIB Vaccinations" )
				or exists ( "Numerator Compliant HIB Conditions" )
		)
		and ( exists ( "Hepatitis B Vaccinations" )
				or exists ( "Numerator Compliant Hepatitis B Conditions" )
		)
		and ( exists ( "VZV Vaccinations" )
				or exists ( "Numerator Compliant Varicella Zoster Conditions" )
		)
		and ( exists ( "Pneumococcal Conjugate Vaccinations" )
				or exists ( "Numerator Compliant Pneumococcal Conjugate Conditions" )
		)
		and ( exists ( "Hepatitis A Vaccinations" )
				or exists ( "Numerator Compliant Hepatitis A Conditions" )
		)
		and ( exists ( "Rotavirus 2 Dose Vaccinations" )
				or exists ( "Numerator Compliant Rotavirus Conditions" )
				or exists ( "Rotavirus 2 or 3 Dose Vaccinations" )
				or exists ( "Rotavirus 3 Dose Vaccinations" )
		)
		and ( exists ( "Influenza Vaccinations" )
				or exists ( "Numerator Compliant Influenza Conditions" )
		)

define "Numerator Compliant DTaP Conditions":
	( ["Diagnosis": "Anaphylactic Reaction to DTaP Vaccine"]
		union ["Diagnosis": "Encephalopathy due to Childhood Vaccination"] ) DPaTConditions
		where AgeInDaysAt(start of DPaTConditions.prevalencePeriod)<= 730

define "Numerator Compliant Hepatitis A Conditions":
	( ( ["Laboratory Test, Performed": "Anti Hepatitis A IgG Antigen Test"] HepAAntigenTest
			where HepAAntigenTest.result as Code in "Positive Finding"
	)
		union ( ["Diagnosis": "Anaphylactic Reaction to Hepatitis A Vaccine"] HepAReaction
				return "Laboratory Test, Performed" { authorDatetime: start of HepAReaction.prevalencePeriod }
		)
		union ( ["Diagnosis": "Hepatitis A"] HepADiagnosis
				return "Laboratory Test, Performed" { authorDatetime: start of HepADiagnosis.prevalencePeriod }
		) ) HepAConditions
		where AgeInDaysAt(HepAConditions.authorDatetime)<= 730

define "Numerator Compliant Hepatitis B Conditions":
	( ( ["Laboratory Test, Performed": "Anti Hepatitis B Virus Surface Ab"] HepBAntigenTest
			where HepBAntigenTest.result as Code in "Positive Finding"
	)
		union ( ["Diagnosis": "Anaphylactic Reaction to Hepatitis B Vaccine"] HepBReaction
				return "Laboratory Test, Performed" { authorDatetime: start of HepBReaction.prevalencePeriod }
		)
		union ( ["Diagnosis": "Anaphylactic Reaction to Common Baker's Yeast"] YeastReaction
				return "Laboratory Test, Performed" { authorDatetime: start of YeastReaction.prevalencePeriod }
		)
		union ( ["Diagnosis": "Hepatitis B"] HepBDiagnosis
				return "Laboratory Test, Performed" { authorDatetime: start of HepBDiagnosis.prevalencePeriod }
		) ) HepBConditions
		where AgeInDaysAt(HepBConditions.authorDatetime)<= 730

define "Numerator Compliant HIB Conditions":
	["Diagnosis": "Anaphylactic Reaction to Hemophilus Influenza B (HiB) Vaccine"] HIBReaction
		where AgeInDaysAt(start of HIBReaction.prevalencePeriod)<= 730

define "Numerator Compliant Influenza Conditions":
	( ["Diagnosis": "Anaphylactic Reaction to Influenza Vaccine"]
		union ["Diagnosis": "Malignant Neoplasm of Lymphatic and Hematopoietic Tissue"]
		union ["Diagnosis": "Anaphylactic Reaction to Neomycin"]
		union ["Diagnosis": "HIV"]
		union ["Diagnosis": "Disorders of the Immune System"] ) InfluenzaConditions
		where AgeInDaysAt(start of InfluenzaConditions.prevalencePeriod)<= 730

define "Numerator Compliant IVP Conditions":
	( ["Diagnosis": "Anaphylactic Reaction to Inactivated Polio Vaccine (IPV)"]
		union ["Diagnosis": "Anaphylactic Reaction to Streptomycin"]
		union ["Diagnosis": "Anaphylactic Reaction to Polymyxin"]
		union ["Diagnosis": "Anaphylactic Reaction to Neomycin"] ) IVPConditions
		where AgeInDaysAt(start of IVPConditions.prevalencePeriod)<= 730

define "Numerator Compliant MMR Conditions":
	( ["Diagnosis": "Disorders of the Immune System"]
		union ["Diagnosis": "HIV"]
		union ["Diagnosis": "Malignant Neoplasm of Lymphatic and Hematopoietic Tissue"]
		union ["Diagnosis": "Anaphylactic Reaction to Neomycin"] ) MMRConditions
		where AgeInDaysAt(start of MMRConditions.prevalencePeriod)<= 730

define "Numerator Compliant Pneumococcal Conjugate Conditions":
	["Diagnosis": "Anaphylactic Reaction to Pneumococcal Conjugate Vaccine"] PCVReaction
		where AgeInDaysAt(start of PCVReaction.prevalencePeriod)<= 730

define "Numerator Compliant Rotavirus Conditions":
	( ["Diagnosis": "Anaphylactic Reaction to Rotavirus Vaccine"]
		union ["Diagnosis": "Severe Combined Immunodeficiency"]
		union ["Diagnosis": "Intussusception"] ) RotavirusConditions
		where AgeInDaysAt(start of RotavirusConditions.prevalencePeriod)<= 730

define "Numerator Compliant Varicella Zoster Conditions":
	( ( ["Laboratory Test, Performed": "Varicella Zoster Antibody Test (IgG Antibody Titer)"] Titer
			where Titer.result >= 1.10
	)
		union ( ["Laboratory Test, Performed": "Varicella Zoster Antibody Test (IgG Antibody Presence)"] IgGPresence
				where IgGPresence.result as Code in "Positive Finding"
		)
		union ( ["Diagnosis": "Disorders of the Immune System"] ImmuneDisorder
				return "Laboratory Test, Performed" { authorDatetime: start of ImmuneDisorder.prevalencePeriod }
		)
		union ( ["Diagnosis": "HIV"] HIV
				return "Laboratory Test, Performed" { authorDatetime: start of HIV.prevalencePeriod }
		)
		union ( ["Diagnosis": "Malignant Neoplasm of Lymphatic and Hematopoietic Tissue"] MalignantNeoplasm
				return "Laboratory Test, Performed" { authorDatetime: start of MalignantNeoplasm.prevalencePeriod }
		)
		union ( ["Diagnosis": "Anaphylactic Reaction to Neomycin"] NeomycinReaction
				return "Laboratory Test, Performed" { authorDatetime: start of NeomycinReaction.prevalencePeriod }
		)
		union ( ["Diagnosis": "Varicella Zoster"] VaricellaZoster
				return "Laboratory Test, Performed" { authorDatetime: start of VaricellaZoster.prevalencePeriod }
		) ) VaricellaZosterConditions
		where AgeInDaysAt(VaricellaZosterConditions.authorDatetime)<= 730

define "Pneumococcal Conjugate Immunizations or Procedures":
	( ["Immunization, Administered": "Pneumococcal Conjugate Vaccine"]
		union ( ["Procedure, Performed": "Pneumococcal Conjugate Vaccine Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) PneumococcalVaccination
		where AgeInDaysAt(PneumococcalVaccination.authorDatetime)>= 42
			and AgeInDaysAt(PneumococcalVaccination.authorDatetime)<= 730

define "Pneumococcal Conjugate Vaccinations":
	from
		"Pneumococcal Conjugate Immunizations or Procedures" Vaccination1,
		"Pneumococcal Conjugate Immunizations or Procedures" Vaccination2,
		"Pneumococcal Conjugate Immunizations or Procedures" Vaccination3,
		"Pneumococcal Conjugate Immunizations or Procedures" Vaccination4
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
			and Vaccination4.authorDatetime 1 day or more after Vaccination3.authorDatetime
		return Vaccination1

define "Qualifying Encounter":
	( ["Encounter, Performed": "Office Visit"]
		union ["Encounter, Performed": "Face-to-Face Interaction"]
		union ["Encounter, Performed": "Home Healthcare Services"]
		union ["Encounter, Performed": "Preventive Care, Established Office Visit, 0 to 17"]
		union ["Encounter, Performed": "Preventive Care Services, Initial Office Visit, 0 to 17"] ) QualifyingEncounter
		where QualifyingEncounter.relevantPeriod during "Measurement Period"

define "Rotavirus 2 Dose Immunizations or Procedures":
	( ["Immunization, Administered": "Rotavirus Vaccine (2 dose schedule)"]
		union ( ["Procedure, Performed": "Rotavirus Vaccine (2 dose schedule) Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) TwoDoseRotavirus
		where AgeInDaysAt(TwoDoseRotavirus.authorDatetime)>= 42
			and AgeInDaysAt(TwoDoseRotavirus.authorDatetime)<= 730

define "Rotavirus 2 Dose Vaccinations":
	from
		"Rotavirus 2 Dose Immunizations or Procedures" Vaccination1,
		"Rotavirus 2 Dose Immunizations or Procedures" Vaccination2
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
		return Vaccination1

define "Rotavirus 2 or 3 Dose Vaccinations":
	( "Rotavirus 2 or 3 Dose Vaccinations A"
			union "Rotavirus 2 or 3 Dose Vaccinations B"
			union "Rotavirus 2 or 3 Dose Vaccinations C"
	)

define "Rotavirus 2 or 3 Dose Vaccinations A":
	from
		"Rotavirus 2 Dose Immunizations or Procedures" Vaccination1,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination2,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Rotavirus 2 or 3 Dose Vaccinations B":
	from
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination1,
		"Rotavirus 2 Dose Immunizations or Procedures" Vaccination2,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Rotavirus 2 or 3 Dose Vaccinations C":
	from
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination1,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination2,
		"Rotavirus 2 Dose Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Rotavirus 3 Dose Immunizations or Procedures":
	( ["Immunization, Administered": "Rotavirus Vaccine (3 dose schedule)"]
		union ( ["Procedure, Performed": "Rotavirus Vaccine (3 dose schedule) Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) ThreeDoseRotavirus
		where AgeInDaysAt(ThreeDoseRotavirus.authorDatetime)>= 42
			and AgeInDaysAt(ThreeDoseRotavirus.authorDatetime)<= 730

define "Rotavirus 3 Dose Vaccinations":
	from
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination1,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination2,
		"Rotavirus 3 Dose Immunizations or Procedures" Vaccination3
		where Vaccination2.authorDatetime 1 day or more after Vaccination1.authorDatetime
			and Vaccination3.authorDatetime 1 day or more after Vaccination2.authorDatetime
		return Vaccination1

define "Rubella Indicators":
	( ( ["Laboratory Test, Performed": "Rubella Antibody Test (IgG Antibody Titer)"] Titer
			where Titer.result >= 1.10
	)
		union ( ["Laboratory Test, Performed": "Rubella Antibody Test (IgG Antibody presence)"] IgGPresence
				where IgGPresence.result as Code in "Positive Finding"
		)
		union ( ["Diagnosis": "Rubella"] RubellaDiagnosis
				return "Laboratory Test, Performed" { authorDatetime: start of RubellaDiagnosis.prevalencePeriod }
		) ) Rubella
		where AgeInDaysAt(Rubella.authorDatetime)<= 730

define "VZV Vaccinations":
	( ["Immunization, Administered": "Varicella Zoster Vaccine (VZV)"]
		union ( ["Procedure, Performed": "Varicella Zoster Vaccine (VZV) Administered"] Procedure
				return "Immunization, Administered" { authorDatetime: start of Procedure.relevantPeriod }
		) ) VZVVaccination
		where AgeInDaysAt(VZVVaccination.authorDatetime)<= 730
